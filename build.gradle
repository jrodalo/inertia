plugins {
    id 'org.springframework.boot' version '2.4.2'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'com.github.node-gradle.node' version "2.2.4"
    id 'java'
}

group = 'es.leanmind'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

compileJava {
    options.compilerArgs << "-Xlint:all,-processing" << "-Werror"
}

compileTestJava {
    options.compilerArgs << "-Xlint:all,-processing" << "-Werror"
}

repositories {
    mavenCentral()
}

node {
    download true
    version = "12.18.3"
}

task buildProd(type: NpmTask) {
    dependsOn(npmInstall)
    args = ['run', 'production']
}

bootRun.dependsOn(buildProd)

dependencies {
    // Spring
    implementation 'org.springframework.boot:spring-boot-starter-web'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // Tests
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
    useJUnitPlatform()
}
